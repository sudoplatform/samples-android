apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId 'com.sudoplatform.virtualcardsexample'
        minSdkVersion 24
        targetSdkVersion 31
        versionCode 1
        versionName "5.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility 11
        targetCompatibility 11
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
        pickFirst("META-INF/kotlinx-coroutines-core.kotlin_module")
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'com.sudoplatform:sudouser:14.0.0'
    implementation 'com.sudoplatform:sudoidentityverification:10.0.1'
    implementation 'com.sudoplatform:sudoprofiles:11.0.0'
    implementation 'com.sudoplatform:sudoentitlements:6.0.0'
    implementation 'com.sudoplatform:sudovirtualcards:9.1.1'

    // Stripe
    implementation "com.stripe:stripe-android:20.5.0"

    // Checkout.com
    implementation "com.github.checkout:frames-android:3.1.2"

    // Plaid
    implementation "com.plaid.link:sdk-core:3.10.2"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.nimbusds:nimbus-jose-jwt:9.31"

    def coroutine_version = "1.6.0-native-mt"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    def fragment_version = '1.5.5'
    implementation "androidx.fragment:fragment:$fragment_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.google.android.material:material:1.5.0'
    constraints {
        implementation("com.squareup.okhttp3:okhttp:4.9.3") {
            because("Versions < 4.9.3 has CVE-2021-0341")
        }
        implementation("com.google.android.gms:play-services-basement:18.1.0") {
            because("Versions < 18.1.0 have CVE-2022-1799")
        }
    }

    def androidxTest = "1.5.0"
    def espresso = "3.4.0"
    androidTestImplementation "androidx.test:core:$androidxTest"
    androidTestImplementation "androidx.test:core-ktx:$androidxTest"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.5"
    androidTestImplementation "androidx.test:runner:$androidxTest"
    androidTestImplementation "androidx.test:rules:$androidxTest"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.3.0"
}

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            // Bouncy Castle 1.62 is a transitive dependency of the certificate
            // transparency library that sudouser uses. This can be upgraded transparently
            // to bcprov-jdk15on:1.65, but stripe library above needs bcprov-jdk15to18
            // i.e. different module name, and so we get duplicate classes. Substitute
            // here for the bcprov/bcpkix-jdk15to18 modules
            substitute module("org.bouncycastle:bcprov-jdk15on") with module("org.bouncycastle:bcprov-jdk15to18:1.68")
            substitute module("org.bouncycastle:bcpkix-jdk15on") with module("org.bouncycastle:bcpkix-jdk15to18:1.68")

            // Don't get too far ahead
            substitute module("androidx.fragment:fragment-ktx") with module("androidx.fragment:fragment-ktx:1.2.5")
        }
    }
}
// Kotlin Lint
apply plugin: 'org.jmailen.kotlinter'

kotlinter {
    ignoreFailures = false
    reporters = ['checkstyle', 'plain']
    experimentalRules = false
}

// Vulnerability scanning
if (project.file("${rootProject.projectDir}/util/vulnerability-scan.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/vulnerability-scan.gradle"
}

// Sonarqube code analysis
if (project.file("${rootProject.projectDir}/util/sonarqube.gradle").exists()) {
    ext.sonarProjectKey = "sudo-virtual-cards-example-android"
    ext.sonarProjectName = "sudo-virtual-cards-example-android"
    ext.sonarExclusions = ["*.png", "*.jks", "*.json", "*.key"]
    apply from: "${rootProject.projectDir}/util/sonarqube.gradle"
}
