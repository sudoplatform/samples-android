apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    compileSdk 34
    namespace "com.sudoplatform.virtualcardsexample"
    defaultConfig {
        applicationId 'com.sudoplatform.virtualcardsexample'
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 1
        versionName "9.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
        create("minifiedDebug") {
            initWith(getByName("debug"))
            minifyEnabled true
            versionNameSuffix = " MINIFIED DEBUG"
            matchingFallbacks.add("debug")
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
        pickFirst("META-INF/kotlinx-coroutines-core.kotlin_module")
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'com.sudoplatform:sudouser:18.0.0'
    implementation 'com.sudoplatform:sudoidentityverification:13.0.0'
    implementation 'com.sudoplatform:sudoprofiles:14.0.0'
    implementation 'com.sudoplatform:sudoentitlements:10.0.0'
    implementation 'com.sudoplatform:sudovirtualcards:14.0.3'

    // Stripe
    implementation "com.stripe:stripe-android:20.5.0"

    // Checkout.com
    implementation "com.github.checkout:frames-android:3.1.2"

    // Plaid
    implementation "com.plaid.link:sdk-core:4.3.1"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.nimbusds:nimbus-jose-jwt:9.31"

    def coroutine_version = "1.7.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    def fragment_version = '1.5.5'
    implementation "androidx.fragment:fragment:$fragment_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.google.android.material:material:1.5.0'
    constraints {
        implementation("com.google.android.gms:play-services-basement:18.1.0") {
            because("Versions < 18.1.0 have CVE-2022-1799")
        }
    }

    def androidxTest = "1.5.0"
    def espresso = "3.5.1"
    androidTestImplementation "androidx.test:core:$androidxTest"
    androidTestImplementation "androidx.test:core-ktx:$androidxTest"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.5"
    androidTestImplementation "androidx.test:runner:$androidxTest"
    androidTestImplementation "androidx.test:rules:$androidxTest"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.3.0"
    androidTestImplementation "com.jakewharton.timber:timber:5.0.1"
}

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            // Don't get too far ahead
            substitute module("androidx.fragment:fragment-ktx") using module("androidx.fragment:fragment-ktx:1.2.5")
        }
    }
}
// Kotlin Lint
apply plugin: 'org.jmailen.kotlinter'

kotlinter {
    ignoreFailures = false
    reporters = ['checkstyle', 'plain']
}

// Vulnerability scanning
if (project.file("${rootProject.projectDir}/util/vulnerability-scan.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/vulnerability-scan.gradle"
}

// Sonarqube code analysis
if (project.file("${rootProject.projectDir}/util/sonarqube.gradle").exists()) {
    ext.sonarProjectKey = "sudo-virtual-cards-example-android"
    ext.sonarProjectName = "sudo-virtual-cards-example-android"
    ext.sonarExclusions = ["*.png", "*.jks", "*.json", "*.key"]
    apply from: "${rootProject.projectDir}/util/sonarqube.gradle"
}
