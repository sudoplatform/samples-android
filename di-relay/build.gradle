// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = "1.8.20"
    ext.nav_version = "2.6.0"
    ext.room_version = "2.5.2"
    ext.okhttp_version = "4.11.0"
    repositories {
        maven { url = "https://plugins.gradle.org/m2" }
        google()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jmailen.gradle:kotlinter-gradle:3.13.0"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        if (project.file("${rootProject.projectDir}/util/vulnerability-scan.gradle").exists()) {
            classpath "org.owasp:dependency-check-gradle:8.2.1"
        }
        if (project.file("${rootProject.projectDir}/util/sonarqube.gradle").exists()) {
            classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:4.0.0.2929"
        }
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

if (project.hasProperty("ossIndexUsername")) {
    ext["ossIndexUsername"] = "$ossIndexUsername"
} else {
    ext["ossIndexUsername"] = ""
}

if (project.hasProperty("ossIndexPassword")) {
    ext["ossIndexPassword"] = "$ossIndexPassword"
} else {
    ext["ossIndexPassword"] = ""
}

allprojects {
    repositories {
        mavenLocal()
        maven {
            name = "android-releases"
            url = "https://repo.tools.anonyome.com/repository/android-releases/"
            credentials {
                username = nexusUsername
                password = nexusPassword
            }
        }
        maven {
            name = "android-snapshots"
            url = "https://repo.tools.anonyome.com/repository/android-snapshots/"
            credentials {
                username = nexusUsername
                password = nexusPassword
            }
        }
        google()
        mavenCentral()
        maven {
            // for code-scanner: https://github.com/yuriy-budiyev/code-scanner
            url 'https://jitpack.io'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
