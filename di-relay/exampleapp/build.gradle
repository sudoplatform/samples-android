apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    namespace 'com.sudoplatform.direlayexample'
    compileSdk 33
    defaultConfig {
        applicationId 'com.sudoplatform.direlayexample'
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 1
        versionName "4.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            resources.srcDirs += ['/var/anonyome_key']
        }
        androidTest {
            resources.srcDirs += ['/var/anonyome_key']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
        pickFirst("META-INF/kotlinx-coroutines-core.kotlin_module")
    }
    buildFeatures {
        viewBinding true
    }
}


dependencies {
    // Sudo Platform
    implementation "com.sudoplatform:sudokeymanager:6.0.0"
    implementation "com.sudoplatform:sudodirelay:4.0.0"
    implementation "com.sudoplatform:sudouser:17.0.0"
    implementation 'com.sudoplatform:sudoprofiles:13.0.2'
    implementation 'com.sudoplatform:sudoentitlements:8.0.1'

    // Peer Connection Storage
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Posting to peer
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"

    def coroutine_version = "1.7.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    def fragment_version = "1.6.1"
    implementation "androidx.fragment:fragment:$fragment_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.google.android.material:material:1.9.0'

    androidTestImplementation "androidx.test:core:1.5.0"
    androidTestImplementation "androidx.test:core-ktx:1.5.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.5"
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "androidx.test:rules:1.5.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.3.0" // 3.3.0 due to hamcrest resolution issue
    androidTestImplementation "com.jakewharton.timber:timber:5.0.1"
}

allprojects {
    configurations.configureEach {
        resolutionStrategy {
            // TODO: sqlite-jdbc is a transitive dependency of room-compiler. Remove this when room-compiler no longer contains CVE-2023-32697.
            force "org.xerial:sqlite-jdbc:3.41.2.2"
        }
    }
}

// Kotlin Lint
apply plugin: 'org.jmailen.kotlinter'

kotlinter {
    ignoreFailures = false
    reporters = ['checkstyle', 'plain']
    experimentalRules = false
}

// Vulnerability scanning
if (project.file("${rootProject.projectDir}/util/vulnerability-scan.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/vulnerability-scan.gradle"
}

// Sonarqube code analysis
if (project.file("${rootProject.projectDir}/util/sonarqube.gradle").exists()) {
    ext.sonarProjectKey = "sudo-di-relay-example-android"
    ext.sonarProjectName = "sudo-di-relay-example-android"
    ext.sonarExclusions = ["*.png", "*.jks", "*.json", "*.key"]
    apply from: "${rootProject.projectDir}/util/sonarqube.gradle"
}
